OBJECT Table 50500 Lease Contract Header
{
  OBJECT-PROPERTIES
  {
    Date=28.10.18;
    Time=20:03:19;
    Modified=Yes;
    Version List=PACKT QSG;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               SalesSetup.GET;

               IF "No." = '' THEN BEGIN
                 SalesSetup.TESTFIELD("Lease Contract Nos.");
                 NoSeriesMgt.InitSeries(SalesSetup."Lease Contract Nos.",xRec."No. Series","Starting Date","No.","No. Series");
               END;
             END;

    OnDelete=VAR
               LeaseContractLine@1000 : Record 50501;
             BEGIN
               IF CustomerPaymentsMgt.ContractPaymentsExist(Rec) THEN
                 ERROR(UnprocessedPmtErr,"No.");

               CALCFIELDS("Total Amount");
               IF CustomerPaymentsMgt.CalculateAmountPaidAtDate(Rec,0D) < "Total Amount" THEN
                 IF NOT CONFIRM(DeleteContractQst) THEN
                   ERROR('');

               LeaseContractLine.SETRANGE("Contract No.","No.");
               LeaseContractLine.DELETEALL(TRUE);
             END;

    LookupPageID=Page50500;
    DrillDownPageID=Page50500;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                SalesSetup.GET;
                                                                NoSeriesMgt.TestManual(SalesSetup."Lease Contract Nos.");
                                                                "No. Series" := '';
                                                              END;
                                                               }
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer }
    { 3   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                ValidateDates;
                                                              END;

                                                   NotBlank=Yes }
    { 4   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                ValidateDates;
                                                              END;

                                                   NotBlank=Yes }
    { 5   ;   ;Total Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Lease Contract Line".Amount WHERE (Contract No.=FIELD(No.),
                                                                                                       Item No.=FIELD(Item No. Filter)));
                                                   Editable=No }
    { 6   ;   ;Item No. Filter     ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Item }
    { 7   ;   ;Amount Paid         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Customer Payment".Amount WHERE (Contract No.=FIELD(No.)));
                                                   Editable=No }
    { 8   ;   ;No. Series          ;Code10         }
    { 9   ;   ;Payment Terms Code  ;Code20        ;TableRelation="Contract Payment Terms" }
    { 10  ;   ;Last Invoice Date   ;Date           }
    { 11  ;   ;Invoice No.         ;Code20         }
    { 12  ;   ;Invoice Posted      ;Boolean        }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Invoice No.                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      WrongStartEndDateErr@1000 : TextConst 'ENU=Starting date cannot be later than the ending date';
      SalesSetup@1001 : Record 311;
      NoSeriesMgt@1002 : Codeunit 396;
      CustomerPaymentsMgt@1003 : Codeunit 50504;
      DeleteContractQst@1004 : TextConst 'ENU=Contract is not completely paid. Do you want to delete it?';
      UnprocessedPmtErr@1005 : TextConst 'ENU=Unprocessed payments exist for contract %1. Contract cannot be deleted.';

    LOCAL PROCEDURE ValidateDates@1();
    BEGIN
      IF ("Starting Date" = 0D) OR ("Ending Date" = 0D) THEN
        EXIT;

      IF "Ending Date" < "Starting Date" THEN
        ERROR(WrongStartEndDateErr);
    END;

    BEGIN
    END.
  }
}

